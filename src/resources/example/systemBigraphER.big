# Signature

#physical structure
ctrl Building = 0; #currently no connections to outside world
ctrl Floor = 2; #connections can define upper and lower floors 
##Rooms
ctrl Room = 1; #could define an AC room, or room A in the floor (AC room, room A)
ctrl Hallway = 1;#30; #number of connections to the rooms in the floor
#ctrl Lab = 1; #control refers to labs that has one connection to hallway (12 labs)
#ctrl Room_2 = 2; #control refers to labs that has two connection to hallway (1 lab)
#ctrl Lab_2 = 2; #control refers to labs that has two connection to hallway (1 lab)
#ctrl Room_4 = 4; #control refers to labs that has four connection to hallway (2 labs)
#ctrl Lab_4 = 4; #control refers to labs that has four connection to hallway (2 labs)
#ctrl Toilet = 1; #connection to hallway (3 toilets)
#ctrl Kitchen = 2; #connections to hallway and AC room (1 kitchen)
#ctrl Lounge = 2;# (1 lounge)
#ctrl Stairs = 1; # (2 stairs)
#ctrl ElevatorsArea = 2; # 1 area

##computingDevices
ctrl Elevator = 1; #(7 in total: 6 elevators in elevator area and 1 emergency elevator)
ctrl Desktop = 1; # (15 desktops)
ctrl Server = 1; # (2 servers, 1 in informatics lab the other in the empirical lab)
atomic ctrl SmartLight = 1;
ctrl HVAC = 1;
atomic ctrl FireAlarm = 1;
atomic ctrl Workstation = 1;
ctrl Laptop = 1;
ctrl CardReader = 1;
ctrl CCTV = 1;
ctrl ComputingDevice = 1;
ctrl SmartTV = 1;
ctrl Microphone = 0;
ctrl Camera = 0;

#Digital structure
ctrl IPNetwork = 1; #defines 10 connections to desktops
ctrl BusNetwork = 1; # 5 should be changed to the right number
atomic ctrl Data = 0; # could be packets
atomic ctrl Malware = 0;
atomic ctrl Software = 0; #could be programs/processes, something that executes

#general status
atomic ctrl Status_On = 0;
atomic ctrl Status_Off = 0;

#Actors
ctrl Visitor = 0;
ctrl Actor = 0;
ctrl Employee = 0;

#Connections
ctrl PhysicalConnection = 1;
ctrl IPConnection = 1;
ctrl BusConnection = 1;

#context (defines some constrains over rules)
ctrl Context = 1; #context contains one of the below states of working (workinghours, cardehours, and closedhours)
atomic ctrl WorkingHours = 0; # defines the period between 8:30-19:00 as working hours
atomic ctrl OutsideWorkingHours = 0; #defines the period between 19:00=22:00 as card required to access rooms
atomic ctrl CardRequired = 0; 
atomic ctrl NoCard = 0; 
atomic ctrl ClosedHours = 0; #defines the period between 22:00-8:30 as Closed hours
atomic ctrl ClosedHours_2 = 0; #defines the period between 19:00-8:30 as Closed hours

#keywords for the reaction rules (possible actions in the system)
atomic ctrl VisitorEnterRoom = 0;
atomic ctrl EmployeeEnterRoom = 0;
atomic ctrl VisitorExitRoom = 0;
atomic ctrl EmployeeExitRoom = 0;

atomic ctrl ConnectToDesktopViaIP = 0;
atomic ctrl ConnectToSmartTVViaIP = 0;
atomic ctrl DisconnectFromDesktopViaIP = 0;
atomic ctrl DisconnectFromSmartTVViaIP = 0;

atomic ctrl ConnectToSmartLightViaBus = 0;
atomic ctrl ConnectToHVACViaBus = 0;
atomic ctrl ConnectToFireAlarmViaBus = 0;
atomic ctrl ConnectToComputingDeviceViaBus = 0;

atomic ctrl DisconnectFromSmartLightViaBus = 0;
atomic ctrl DisconnectFromFireAlarmViaBus = 0;
atomic ctrl DisconnectFromHVACViaBus = 0;
atomic ctrl DisconnectFromComputingDeviceViaBus = 0;

atomic ctrl GenerateData = 0;
atomic ctrl CollectData = 0;
atomic ctrl SendData = 0;
atomic ctrl SendMalware = 0;
atomic ctrl DisableHVAC = 0;
#atomic ctrl ChangeAccessToCardNotRequired = 0;
atomic ctrl ChangeContextToOutSideWorkingHours = 0; 
atomic ctrl ChangeContextToWorkingHours = 0;
atomic ctrl TurnOnHVAC = 0;
atomic ctrl TurnOffHVAC = 0;
atomic ctrl TurnOnSmartTV = 0;
atomic ctrl TurnOffSmartTV = 0;
atomic ctrl TurnONTVMicrophone = 0;
atomic ctrl TurnOffTVMicrophone = 0;
atomic ctrl TurnONTVCamera = 0;
atomic ctrl TurnOffTVCamera = 0;
ctrl RulesKeywords = 0;

#initial state

#digital components

#busnetwork states
big busNetwork = BusNetwork{busNet}.1;
big busNetwork_has_data = BusNetwork{busNet}.Data; 

big ipNetwork = IPNetwork{ipNet}.1;

#physical components
big visitor_initial = Visitor.(/a Laptop{a}.Malware);

big hvac = HVAC{busNet}.Status_On;

#common devices found in a room
big computing_devices_in_room = SmartLight{busNet} | hvac;

#different connections defined since a room can have more than one door
big connection_to_hallway = PhysicalConnection{hallway}.NoCard;
#big connection_to_hallway2 = PhysicalConnection{hallway2}.NoCard;
#big connection_to_hallway3 = PhysicalConnection{hallway3}.NoCard;
#big connection_to_hallway4 = PhysicalConnection{hallway4}.NoCard;

big connection_to_hallway_card_required = PhysicalConnection{hallway}.CardRequired;
#big connection_to_hallway2_card_required = PhysicalConnection{hallway2}.CardRequired; 
#big connection_to_hallway3_card_required = PhysicalConnection{hallway3}.CardRequired;
#big connection_to_hallway4_card_required = PhysicalConnection{hallway4}.CardRequired;


big connection_to_elevators_area = PhysicalConnection{elevator}.NoCard;

big office_researcher = Room{hallway}.( computing_devices_in_room | 
Desktop{ipNet}.1 | Desktop{ipNet}.1 | Employee.1);

big office_manager = Room{hallway}.( computing_devices_in_room | Laptop{ipNet}.1 | Server{busNet}.1);

big smartTV = SmartTV{ipNet}.(Camera.Status_Off | Microphone.Status_Off | Status_Off);
big smartTV_off = SmartTV{ipNet}.(Status_Off | id);
big smartTV_on = SmartTV{ipNet}.(Status_On | id);
big smartTV_microphone_on = SmartTV{ipNet}.(Microphone.Status_On | Status_On | id);
big smartTV_microphone_off = SmartTV{ipNet}.(Microphone.Status_Off | Status_On | id);
big smartTV_camera_on = SmartTV{ipNet}.(Camera.Status_On | Status_On | id);
big smartTV_camera_off = SmartTV{ipNet}.(Camera.Status_Off | Status_On | id);

big office_BN = Room{hallway}.( computing_devices_in_room | 
Employee.1 | Laptop{ipNet}.1 | smartTV | Server{busNet}.1);

big kitchen = Room{hallway}
.(computing_devices_in_room);

#big hvac_room = Room{hallway1}.(hvac | SmartLight{busNet} | CardReader{busNet}.1);

big toilet = Room{hallway}.(computing_devices_in_room);

big elevators_area = Room{hallway}.(Elevator{elevator}.1 | CardReader{busNet}.1 | visitor_initial); 

big meeting_room = Room{hallway}.(computing_devices_in_room);


big hallway = Hallway{hallway}
.(CCTV{busNet}.1 |  computing_devices_in_room);

big connections = connection_to_hallway | connection_to_hallway_card_required;

big context = Context{c}.(WorkingHours);

big actions = RulesKeywords.(VisitorEnterRoom| VisitorExitRoom | EmployeeEnterRoom | EmployeeExitRoom | 
ConnectToDesktopViaIP | ConnectToSmartTVViaIP | DisconnectFromDesktopViaIP| DisconnectFromSmartTVViaIP | 
ConnectToComputingDeviceViaBus| ConnectToFireAlarmViaBus| ConnectToHVACViaBus|ConnectToSmartLightViaBus | 
DisconnectFromComputingDeviceViaBus | DisconnectFromFireAlarmViaBus |DisconnectFromHVACViaBus| DisconnectFromSmartLightViaBus | 
 SendData | SendMalware| DisableHVAC  | 
ChangeContextToOutSideWorkingHours | ChangeContextToWorkingHours |
 TurnOnHVAC | TurnOffHVAC | TurnOnSmartTV | TurnOffSmartTV | 
 TurnONTVMicrophone | TurnONTVCamera | TurnOffTVMicrophone | TurnOffTVCamera | GenerateData | CollectData);
 
#initial state
big research_centre2 = Building.( 
Floor{lowerFloor, upperFloor}.(
office_researcher |
office_researcher |
office_researcher |
office_researcher |
office_researcher |
office_researcher |
office_researcher |
office_researcher |
office_BN |
office_manager |
kitchen         |
toilet          |
toilet          |
toilet          |
elevators_area  |
meeting_room    |
meeting_room    |
hallway )
|
busNetwork_has_data |
ipNetwork 
)
|| 
connections
||
context
||
actions
;        

#visitor states
big visitor = Visitor.id;
big employee = Employee.id;
big visitor_laptop = Visitor.(/a Laptop{a} | id);
big visitor_connected_to_IP = Visitor.(Laptop{ipNet}.id | id);
big visitor_connected_to_bus = Visitor.(Laptop{busNet}.id | id);
big visitor_connected_to_bus_has_data = Visitor.(Laptop{busNet}.(id | Data) | id);
big visitor_connected_to_bus_with_malware = Visitor.(Laptop{busNet}.(id | Malware) | id);

#actor states
big employee_connected_to_IP = Actor.(ComputingDevice{ipNet} | id);
big employee_connected_to_bus = Employee.(ComputingDevice{busNet} | id);
big employee_connected_to_bus_with_data = Employee.(ComputingDevice{busNet}.(id | Data) | id);

#offender target
big malware_target = HVAC{busNet} | Server{ipNet} ;
big malware_target_infected = HVAC{busNet}.(Malware | id) | Server{ipNet} ;
big malware_target_disabled = HVAC{busNet}.(Malware | Status_Off) | Server{ipNet} ;

big hallway_visitor = Hallway{hallway}.(id | visitor);
big hallway_employee = Hallway{hallway}.(id | employee);
big hallway_general = Hallway{hallway}.id;

#context
big context_working_hours = Context{c}.WorkingHours;
big context_outside_working_hours = Context{c}.OutsideWorkingHours;

#room in general
big room_general = Room{hallway};

#enter room action
react enter_room_during_working_hours = 
hallway_visitor | room_general || connection_to_hallway || context_working_hours || RulesKeywords.(VisitorEnterRoom | id)
->
hallway_general | Room{hallway}.(visitor | id) || connection_to_hallway || context_working_hours  || RulesKeywords.(id)
;

#exit room action
react exit_room = 
hallway_general | Room{hallway}.(visitor | id) || connection_to_hallway || RulesKeywords.(VisitorExitRoom | id)
->
hallway_visitor | Room{hallway}.id  || connection_to_hallway || RulesKeywords.(id)
;

#employee enter room action
react employee_enter_room_during_working_hours = 
hallway_employee | room_general || connection_to_hallway || context_working_hours || RulesKeywords.(EmployeeEnterRoom | id)
->
hallway_general | Room{hallway}.(employee | id) || connection_to_hallway || context_working_hours  || RulesKeywords.(id)
;

#exit room action
react employee_exit_room = 
hallway_general | Room{hallway}.(employee | id) || connection_to_hallway || RulesKeywords.(EmployeeExitRoom | id)
->
hallway_employee | Room{hallway}.id  || connection_to_hallway || RulesKeywords.(id)
;

#connect to desktop
react connect_to_desktop = 
visitor_laptop | Desktop{ipNet} || RulesKeywords.(ConnectToDesktopViaIP | id)
->
visitor_connected_to_IP | Desktop{ipNet} || RulesKeywords.(id)
;

#disconnect to desktop
react disconnect_from_desktop = 
visitor_connected_to_IP | Desktop{ipNet} || RulesKeywords.(DisconnectFromDesktopViaIP | id)
->
visitor_laptop | Desktop{ipNet} || RulesKeywords.(id)
;

#connect to smart light
react connect_to_smartlight = 
visitor_laptop | SmartLight{busNet} || RulesKeywords.(ConnectToSmartLightViaBus | id)
->
visitor_connected_to_bus | /busNet SmartLight{busNet} || RulesKeywords.(id)
;

#disconnect from smart light
react disconnect_from_smartlight = 
visitor_connected_to_bus | /busNet SmartLight{busNet} || RulesKeywords.(DisconnectFromSmartLightViaBus | id)
->
visitor_laptop | SmartLight{busNet} || RulesKeywords.(id)
;

#connect to hvac
react connect_to_hvac = 
visitor_laptop | HVAC{busNet} || RulesKeywords.(ConnectToHVACViaBus | id)
->
visitor_connected_to_bus | /busNet HVAC{busNet} || RulesKeywords.(id)
;


#disconnect from hvac
react disconnect_from_hvac = 
visitor_connected_to_bus | /busNet HVAC{busNet} || RulesKeywords.(DisconnectFromHVACViaBus | id)
->
visitor_laptop | HVAC{busNet} || RulesKeywords.(id)
;

#connect to SmartTV
react connect_to_smartTV = 
visitor_laptop | SmartTV{ipNet} || RulesKeywords.(ConnectToSmartTVViaIP | id)
->
visitor_connected_to_IP | /ipNet SmartTV{ipNet} || RulesKeywords.(id)
;


#disconnect from SmartTV
react disconnect_from_smartTV = 
visitor_connected_to_IP | /ipNet SmartTV{ipNet} || RulesKeywords.(DisconnectFromSmartTVViaIP | id)
->
visitor_laptop | SmartTV{ipNet} || RulesKeywords.(id)
;

#connect to fire alarm
react connect_to_firealarm = 
visitor_laptop | FireAlarm{busNet} || RulesKeywords.(ConnectToFireAlarmViaBus | id)
->
visitor_connected_to_bus | /busNet FireAlarm{busNet} || RulesKeywords.(id)
;

#disconnect from fire alarm
react disconnect_from_firealarm = 
visitor_connected_to_bus | /busNet FireAlarm{busNet} || RulesKeywords.(DisconnectFromFireAlarmViaBus | id)
->
visitor_laptop | FireAlarm{busNet} || RulesKeywords.(id)
;

#connect to computing device
react connect_to_computingdevice = 
visitor_laptop | ComputingDevice{busNet} || RulesKeywords.(ConnectToComputingDeviceViaBus | id)
->
visitor_connected_to_bus | /busNet ComputingDevice{busNet} || RulesKeywords.(id)
;

#disconnect from computing device
react disconnect_from_computingdevice = 
visitor_connected_to_bus | /busNet ComputingDevice{busNet} || RulesKeywords.(DisconnectFromComputingDeviceViaBus | id)
->
visitor_laptop | ComputingDevice{busNet} || RulesKeywords.(id)
;

#if employee laptop is connected then add a Data to it [actor action]
react employee_generate_data = 
employee_connected_to_bus || RulesKeywords.(GenerateData | id)
->
employee_connected_to_bus_with_data || RulesKeywords.(id);

#if employee laptop is connected then send Data (could be software) to hvac [actor action]
react employee_send_data = 
employee_connected_to_bus_with_data  || HVAC{busNet} || RulesKeywords.(SendData | id)
->
employee_connected_to_bus  ||  HVAC{busNet}.(Data |  id) || RulesKeywords.(id)
;

#turn hvac on
react turn_on_hvac = 
HVAC{busNet}.Status_Off || RulesKeywords.(TurnOnHVAC | id)
->
HVAC{busNet}.Status_On || RulesKeywords.(id)
;

#turn hvac off
react turn_off_hvac = 
HVAC{busNet}.Status_On || RulesKeywords.(TurnOffHVAC | id)
->
HVAC{busNet}.Status_Off || RulesKeywords.(id)
;

#turn smart tv on
react turn_on_smartTV = 
employee | smartTV_off || RulesKeywords.(TurnOnSmartTV | id)
->
employee | smartTV_on || RulesKeywords.(id)
;

#turn smart tv off
react turn_off_smartTV = 
employee | smartTV_on || RulesKeywords.(TurnOffSmartTV | id)
->
employee | smartTV_off || RulesKeywords.(id)
;

#if visitor laptop is connected then send malware to hvac, which its container has server [malicious action]
react visitor_send_malware = 
visitor_connected_to_bus_with_malware  || malware_target || RulesKeywords.(SendMalware | id)
->
visitor_connected_to_bus || malware_target_infected || RulesKeywords.(id)
;

#malware disables hvac [malicious action]
react disable_hvac = 
malware_target_infected || RulesKeywords.(DisableHVAC | id)
->
malware_target_disabled || RulesKeywords.(id)
@[1,2]
;

#change context
react change_to_outside_working_hours = 
context_working_hours || RulesKeywords.(ChangeContextToOutSideWorkingHours | id)
->
context_outside_working_hours || RulesKeywords.(id)
;

react change_to_working_hours = 
context_outside_working_hours || RulesKeywords.(ChangeContextToWorkingHours | id)
->
context_working_hours || RulesKeywords.(id)
;

#turn on smart tv microphone
react turn_on_TV_microphone = 
smartTV_microphone_off || RulesKeywords.(TurnONTVMicrophone | id)
->
smartTV_microphone_on || RulesKeywords.(id)
;

#turn off smart tv microphone
react turn_off_TV_microphone = 
smartTV_microphone_on || RulesKeywords.(TurnOffTVMicrophone | id)
->
smartTV_microphone_off || RulesKeywords.(id)
;

#turn on smart tv camera
react turn_on_TV_camera = 
smartTV_camera_off || RulesKeywords.(TurnONTVCamera | id)
->
smartTV_camera_on || RulesKeywords.(id)
;

#turn off smart tv camera
react turn_off_TV_camera = 
smartTV_camera_on || RulesKeywords.(TurnOffTVCamera | id)
->
smartTV_camera_off || RulesKeywords.(id)
;

#collect data action
react collect_data = 
visitor_connected_to_bus || busNetwork_has_data || RulesKeywords.(CollectData | id)
->
visitor_connected_to_bus_has_data || busNetwork || RulesKeywords.(id)
;

#Reactive system
begin brs
  init research_centre2;
  rules = 
  [ 
  {
    enter_room_during_working_hours, exit_room
  , employee_enter_room_during_working_hours, employee_exit_room    
  , connect_to_desktop, disconnect_from_desktop
  , connect_to_smartlight, disconnect_from_smartlight
  , connect_to_hvac, disconnect_from_hvac
  , connect_to_smartTV, disconnect_from_smartTV
  , connect_to_firealarm, disconnect_from_firealarm
  , connect_to_computingdevice, disconnect_from_computingdevice
  , employee_generate_data
  , employee_send_data
  , visitor_send_malware
  , disable_hvac
  , change_to_working_hours, change_to_outside_working_hours
  , turn_on_hvac, turn_off_hvac
  , turn_on_smartTV, turn_off_smartTV
  , turn_on_TV_camera, turn_off_TV_camera
  , turn_on_TV_microphone, turn_off_TV_microphone
  , collect_data
  } 
  ];

end








